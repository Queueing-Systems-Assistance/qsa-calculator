plugins {
  id 'java'
  id 'java-library'
  id 'io.spring.dependency-management' version '1.0.10.RELEASE'
  id 'checkstyle'
}

ext {
  projectGroup = 'com.unideb.qsa'
  projectVersion = project.getProperties().get('release.version') ?: 'RELEASE.VERSION.UNKNOWN'
  /* Secrets */
  // Note: Use your username and token
  githubToken = System.getenv('GITHUB_TOKEN') ?: 'YOUR_TOKEN'
  githubUsername = System.getenv('GITHUB_USERNAME') ?: 'YOUR_USERNAME'
  print('Release version [' + projectVersion + ']')
}

allprojects {

  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'checkstyle'

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
      url 'https://jitpack.io'
    }
    maven {
      url 'https://maven.pkg.github.com/queueing-systems-assistance/qsa'
      credentials {
        username = githubUsername
        password = githubToken
      }
    }
  }

  dependencyManagement {
    dependencies {
      /* QSA */
      dependency 'com.unideb.qsa:qsa-config-domain:1.0.11'
      dependency 'com.unideb.qsa:qsa-config-resolver:1.0.9'
      /* DEPENDENCY */
      imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.4.1'
      }
      dependency 'io.cucumber:cucumber-java:6.9.1'
      dependency 'io.cucumber:cucumber-spring:6.9.1'
      dependency 'io.cucumber:cucumber-testng:6.9.1'
      dependency 'net.logstash.logback:logstash-logback-encoder:6.6'
      dependency 'io.micrometer:micrometer-registry-prometheus:1.6.2'
      dependency 'org.apache.commons:commons-math3:3.6.1'
      dependency 'org.mockito:mockito-core:3.6.28'
      dependency 'org.slf4j:jcl-over-slf4j:1.7.30'
      dependency 'org.slf4j:slf4j-api:1.7.30'
      dependency 'org.springframework:spring-context:5.3.2'
      dependency 'org.springframework.boot:spring-boot-starter-web:2.4.1'
      dependency 'org.springframework.boot:spring-boot-starter-webflux:2.4.0'
      dependency 'org.springframework.boot:spring-boot-starter-actuator:2.4.1'
      dependency 'org.springframework.boot:spring-boot-starter-test:2.4.1'
      dependency 'org.testng:testng:7.3.0'
    }
  }

  checkstyle {
    toolVersion = '8.38'
    configFile = file("${rootDir}/support/code-formatting/checkstyle/checkstyle.xml")
    configDirectory = file("${rootDir}/support/code-formatting/checkstyle/")
    showViolations = true
    ignoreFailures = false
  }

  group = projectGroup
  version = projectVersion
  sourceCompatibility = '11'

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  tasks.withType(Checkstyle) {
    reports {
      xml.enabled false
      html.enabled false
    }
  }

  test {
    useTestNG()
    scanForTestClasses = false
    testLogging {
      afterSuite { desc, result ->
        if (!desc.parent) {
          println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
        }
      }
    }
  }
}
